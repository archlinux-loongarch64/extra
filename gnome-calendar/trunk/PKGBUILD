# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=42.0
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server libsoup libadwaita libgweather-4 geoclue
         geocode-glib gsettings-desktop-schemas gnome-control-center)
makedepends=(git meson)
groups=(gnome)
options=(debug)
_commit=f49bec413e41a0d41be8fc59056e3029971ae846  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#commit=$_commit"
        0001-weather-service-Reduce-location-accuracy.patch)
sha256sums=('SKIP'
            '45441d15d60131ea2a8bd20f04aaf803ebb3e351800c1a577e77a737f770a111')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/gnome-calendar/-/merge_requests/216
  git apply -3 ../0001-weather-service-Reduce-location-accuracy.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
