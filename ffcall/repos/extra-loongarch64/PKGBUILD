# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Michael Baehr <usemike@spamblocked.com>

pkgname=ffcall
pkgver=2.4.7
pkgrel=1
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('x86_64' 'loongarch64')
url="https://www.gnu.org/software/libffcall/"
license=('GPL2')
options=('!makeflags' 'staticlibs')
_commit=e6de60416ca832506ae57cef62b48cc8dd5f9a00
source=("git+https://git.savannah.gnu.org/git/libffcall.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/lib${pkgname}"

  [ "$CARCH" = "x86_64" ] && CONFIGFLAG="--with-pic"
  autoreconf -fi
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  make
}

check() {
  cd "${srcdir}/lib${pkgname}"
  make check
}

package() {
  cd "${srcdir}/lib${pkgname}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  make DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
