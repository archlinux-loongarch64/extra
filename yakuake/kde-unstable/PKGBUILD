# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: leeghoofd <abcdefg@solcon.nl>

pkgname=yakuake
pkgver=22.03.90
pkgrel=1
pkgdesc='A drop-down terminal emulator based on KDE konsole technology'
arch=(x86_64)
url='https://apps.kde.org/yakuake/'
license=(GPL)
depends=(konsole kwayland hicolor-icon-theme)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b6f4d8e79b52ec687e5203ad63aa3866a3a51d03860cd19a86d04ddf28f312ac'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
