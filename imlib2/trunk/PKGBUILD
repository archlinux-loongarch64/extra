# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

pkgname=imlib2
pkgver=1.8.1
pkgrel=1
pkgdesc='Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support'
url='https://sourceforge.net/projects/enlightenment/'
arch=('x86_64')
license=('BSD')
options=('debug')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg-turbo' 'libwebp' 'libheif' 'librsvg' 'xz')
source=("https://downloads.sourceforge.net/project/enlightenment/imlib2-src/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('68366298eb3badc7e76ca3b8d73ac0d1489384061cd523cf58bbc9059720b835a82bed6fe87fcf53ed1b43de0c1b6a9e270e25463a30709b568c5b2f3fd06835')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/imlib2 \
    --x-libraries=/usr/lib \
    --enable-amd64
  make
}


check() {
  cd "${pkgname}-${pkgver}"
  make check
}


package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
